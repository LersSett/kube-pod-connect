#compdef kube-pod-connect

__namespaces () {
  local -a _namespaces
  if [[ " ${words[*]} " == *" -f "* ]] || [[ " ${words[*]} " == *" --force "* ]]; then
    _namespaces=($(kube-pod-connect -f -n))
  else
    _namespaces=($(kube-pod-connect -n))
  fi
  typeset -U _namespaces
  _describe 'namespaces' _namespaces
}


__pod_names() {
  local -a _pod_names
  # TODO: no cache if flag --force
  _pod_names=($(kube-pod-connect -p $words[1]))
  typeset -U _pod_names
  _describe 'pod_names' _pod_names
}

__commands() {
  local -a _commands
  _commands=(
  'bash:bash'
  'sh:sh'
  'ash:ash'
  )
  typeset -U _commands
  _describe 'commands' _commands
}

_kube_exec() {
  _arguments \
    {-f,--force}'[Force update namespace or pod list]' \
    {-n,--namespaces}'[List namespaces]' \
    {-p,--pod-names}'[Pod names for namespace]:namespace:__namespaces' \
    "1: :{__namespaces}" \
    '*:: :->args'

    case $state in
    args)

      _arguments \
        "1: :{__pod_names}" \
        '*:: :->args'

      case $state in
      args)

        _arguments \
          "1: :{__commands}"
        ;;
      esac
      ;;
    esac
}


_kube_exec


